apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.github.alunegov.tchart"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

def keystorePropertiesFile = rootProject.file("${System.properties['user.home']}/.keystore_props/android_alunegov_keystore.properties")
if (keystorePropertiesFile.canRead()) {
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android.signingConfigs.release.keyAlias = keystoreProperties['keyAlias']
    android.signingConfigs.release.keyPassword = keystoreProperties['keyPassword']
    android.signingConfigs.release.storeFile = file(keystoreProperties['storeFile'])
    android.signingConfigs.release.storePassword = keystoreProperties['storePassword']
} else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:23.3.0'
    implementation 'org.jetbrains:annotations-java5:15.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20180130'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    debugImplementation "com.github.brianPlummer:tinydancer:0.1.2"
    releaseImplementation "com.github.brianPlummer:tinydancer-noop:0.1.2"
    testImplementation "com.github.brianPlummer:tinydancer-noop:0.1.2"
}
